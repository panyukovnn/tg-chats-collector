plugins {
    id 'java'
    id 'jacoco'
    id 'com.palantir.git-version' version '3.1.0'
    id 'io.quarkus' version "3.17.0"
}

group = 'ru.panyukovnn'
version = 'dev'

java.targetCompatibility = JavaVersion.VERSION_21
java.sourceCompatibility = JavaVersion.VERSION_21

repositories {
    mavenLocal()
    mavenCentral()
    maven { url "https://mvn.mchv.eu/repository/mchv/" }
}

dependencies {
    implementation project(':tg-chats-collector-api')
    implementation enforcedPlatform("io.quarkus.platform:quarkus-bom:3.17.0")
    implementation 'io.quarkus:quarkus-arc'
    implementation 'io.quarkus:quarkus-rest'
    implementation 'io.quarkus:quarkus-rest-jackson'
    implementation 'io.quarkus:quarkus-hibernate-validator'
    implementation 'io.quarkus:quarkus-jackson'
    implementation 'io.quarkus:quarkus-config-yaml'
    implementation 'io.quarkus:quarkus-smallrye-health'

    implementation 'org.telegram:telegrambots:6.5.0'
    implementation 'org.telegram:telegrambots-meta:6.5.0'
    implementation 'org.telegram:telegrambotsextensions:6.5.0'

    implementation 'io.swagger.core.v3:swagger-annotations:2.2.8'

    implementation platform('it.tdlight:tdlight-java-bom:3.4.0+td.1.8.26')
    implementation group: 'it.tdlight', name: 'tdlight-java'
    implementation group: 'it.tdlight', name: 'tdlight-natives', classifier: 'macos_arm64'
    implementation group: 'it.tdlight', name: 'tdlight-natives', classifier: 'linux_amd64_clang_ssl3'
    implementation group: 'it.tdlight', name: 'tdlight-natives', classifier: 'linux_amd64_gnu_ssl1'
    implementation group: 'it.tdlight', name: 'tdlight-natives', classifier: 'linux_amd64_gnu_ssl3'
    implementation group: 'it.tdlight', name: 'tdlight-natives', classifier: 'linux_arm64_clang_ssl3'
    implementation group: 'it.tdlight', name: 'tdlight-natives', classifier: 'linux_armhf_gnu_ssl1'

    implementation "org.jsoup:jsoup:1.20.1"

    compileOnly 'org.projectlombok:lombok:1.18.30'
    annotationProcessor 'org.projectlombok:lombok:1.18.30'
    testCompileOnly 'org.projectlombok:lombok:1.18.30'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.30'

    testImplementation "com.h2database:h2"
    testImplementation 'io.quarkus:quarkus-junit5'
    testImplementation 'io.rest-assured:rest-assured'
}

test {
    useJUnitPlatform()
}

jacocoTestReport {
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: [
                    "**/config/*",
                    "**/property/*",
                    "**/exception/*"
            ])
        }))
    }
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                minimum = 0.40
            }
        }
    }
}

// Задача для копирования нативных библиотек
task copyNativeLibs(type: Copy) {
    dependsOn configurations.runtimeClasspath
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    into layout.buildDirectory.dir('native-libs')
    include '**/libtdjni*.so'
    include '**/libtdjni*.dylib'
    include '**/tdjni*.so'
    include '**/tdjni*.dylib'
    eachFile {
        path = name
    }
    includeEmptyDirs = false
}

quarkusBuild.dependsOn copyNativeLibs

tasks.withType(Test).configureEach {
    systemProperty "file.encoding", "UTF-8"
    systemProperty "java.util.logging.manager", "org.jboss.logmanager.LogManager"
}
